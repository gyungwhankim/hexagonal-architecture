plugins {
    id 'java-library'
    id "java-test-fixtures"
    id("org.springframework.boot") version "3.1.2" apply(false)
    id("io.spring.dependency-management") version "1.1.2" apply(false)
}

subprojects {
    group = 'com.torder'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    // 공통 관리 종속성
    dependencies {

        testImplementation 'net.datafaker:datafaker:2.0.1'

        //querydsl
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
        annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
        annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        // mapstruct
        implementation 'org.mapstruct:mapstruct:1.5.3.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0' // v1.18.16+ 부터

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"
        testAnnotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0' // v1.18.16+ 부터

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.testcontainers:testcontainers:1.18.3'

    }

    test {
        useJUnitPlatform()
    }
}

project(':service-base:service-combine') {
    dependencies {
        api project(':service-base:service-goods-base:service-goods-web')
        api project(':service-base:service-goods-base:service-goods-repository')
    }
}

project(':service-base:service-goods-base:service-goods-web') {
    dependencies {
        api project(':service-base:service-common-web')
        api project(':service-base:service-goods-base:service-goods-application')
    }
}

project(':service-base:service-goods-base:service-goods-application') {
    dependencies {
        api project(':service-base:service-common-application')
    }
}

project(':service-base:service-goods-base:service-goods-repository') {
    dependencies {
        api project(':service-base:service-goods-base:service-goods-application')
        api project(':service-base:service-common-repository')
    }
}
